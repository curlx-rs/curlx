request = { request_line ~ headers? ~ (NEWLINE ~ body)? }

request_line = _{ method ~ SPACE_SEPARATOR ~ uri ~ SPACE_SEPARATOR+ ~ version ~ SPACE_SEPARATOR* ~ NEWLINE }
uri = { (!SPACE_SEPARATOR ~ ANY)+ }
method = { "GET" | "POST" | "PUT" | "DELETE" }
version = { "HTTP/" ~ (ASCII_DIGIT | ".")+ }

headers = { header+ }
header = { header_name ~ SPACE_SEPARATOR* ~ ":" ~ SPACE_SEPARATOR* ~ header_value ~ SPACE_SEPARATOR* ~ NEWLINE }
header_name = { (!(":" | NEWLINE) ~ ANY)+ }
header_value = { (!NEWLINE ~ ANY)+ }

body = { !NEWLINE ~ (!delimiter ~ ANY)* }
delimiter = { "#"{3} ~ (!NEWLINE ~ ANY)+ ~ SPACE_SEPARATOR* ~ NEWLINE+ }

file = { SOI ~ (delimiter? ~ request) ~ (delimiter ~ request)* ~ EOI}
